plugins {
    id 'java-library'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    //withSourcesJar()
}

apply plugin: 'java'
repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'
    implementation 'org.apache.httpcomponents:httpclient:4.5.9'
    implementation 'org.jetbrains:annotations:16.0.2'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.0'
    testImplementation 'junit:junit:4.12'
}

// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}

// Deploying
apply plugin: 'maven'

// Add Javadoc JAR and sources JAR to artifact
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
artifacts {
    archives javadocJar, sourcesJar
}

// Configure group ID, artifact ID, and version
group = "com.smileidentity"
archivesBaseName = "smile-identity-core"
version = "1.0.10" //last used snapshot 1.0.5-SNAPSHOT

// Build, sign, and upload
uploadArchives {
    repositories {
        mavenDeployer {

            // Sign POM
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            // Destination
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            // Add required metadata to POM
            pom.project {
                name 'smile-identity-core'
                description 'The Official Smile Identity library'
                packaging 'jar'
                url 'https://github.com/smileidentity/smile-identity-core-java'

                scm {
                    connection 'scm:git:git://github.com/smileidentity/smile-identity-core-java.git'
                    developerConnection 'scm:git:ssh://github.com:smileidentity/smile-identity-core-java.git'
                    url 'https://github.com/smileidentity/smile-identity-core-java/tree/master'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
						id = 'Support'
                        name = 'Smile Identity'
                        email = 'support@smileidentity.com'
                        organization = 'Smile Identity'
                        organizationUrl = 'https://www.smileidentity.com'
                    }
                }	
            }
        }
    }
}